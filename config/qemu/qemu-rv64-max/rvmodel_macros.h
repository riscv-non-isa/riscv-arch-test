# rvmodel_macros.h
# DUT-specific macro definitions for Qemu
# Jordan Carlin jcarlin@hmc.edu Feb 2026
# SPDX-License-Identifier: BSD-3-Clause

#ifndef _RVMODEL_MACROS_H
#define _RVMODEL_MACROS_H

#define RVMODEL_DATA_SECTION \
        .pushsection .data,"aw",@progbits;                             \
        .p2align 3; .global _semihost_exit_pass;                          \
        _semihost_exit_pass: .dword 0x20026; .dword 0;                  \
        .p2align 3; .global _semihost_exit_fail;                          \
        _semihost_exit_fail: .dword 0x20023; .dword 1;                  \
        .popsection

#define STANDARD_SM_SUPPORTED

##### STARTUP #####

# Perform boot operations. Can be empty or left undefined unless needed for
# DUT-specific behavior such as turning on a memory controller or
# initializing custom state.
//#define RVMODEL_BOOT

// Custom RVMODEL_BOOT_TO_MMODE overrides default RVTEST_BOOT_TO_MMODE
// if defined.  For most DUTs, the default should work and this macro
// should not be defined.  If no M-mode or CSRs are implemented, define this
// macro as blank to bypass the boot process.  If a nonconforming
// M-mode is implemented, define this macro to set up the necessary
// state in a fashion similar to RVTEST_BOOT_TO_MMODE.
//#define RVMODEL_BOOT_TO_MMODE


##### TERMINATION #####

# QEMU uses semihosting to terminate the simulation.
# Semihosting triggers commands by using a special sequence of three instructions.
# See https://docs.riscv.org/reference/platform-software/semihosting/_attachments/riscv-semihosting.pdf
# for details. Note: QEMU semihosting sequences cannot cross page boundaries.
# The semihosting codes used here are described below. The subsequent link has more details on all semihosting codes.
# On RV64, SYS_EXIT expects a1 = pointer to {reason, subcode} parameter block.
# On RV32, SYS_EXIT expects a1 = reason value directly.
# 0x20026 = ADP_Stopped_ApplicationExit (exit code 0)
# 0x20023 = ADP_Stopped_InternalError (exit code 1)
# https://github.com/ARM-software/abi-aa/blob/main/semihosting/semihosting.rst

#if __riscv_xlen == 64

  # Terminate test with a pass indication.
  # When the test is run in simulation, this should end the simulation.
  #define RVMODEL_HALT_PASS     \
    .option push               ;\
    .option norvc              ;\
    /* load exit code (see above) */ \
    la a1, _semihost_exit_pass ;\
    li a0, 0x18                ;\
    /* align to ensure semihosting instructions don't cross a page boundary */ \
    .balign 16                 ;\
    /* semihosting special sequence */ \
    slli x0, x0, 0x1f          ;\
    ebreak                     ;\
    srai x0, x0, 7             ;\
    .option pop

  # Terminate test with a fail indication.
  # When the test is run in simulation, this should end the simulation.
  #define RVMODEL_HALT_FAIL     \
    .option push               ;\
    .option norvc              ;\
    /* load exit code (see above) */ \
    la a1, _semihost_exit_fail ;\
    li a0, 0x18                ;\
    /* align to ensure semihosting instructions don't cross a page boundary */ \
    .balign 16                 ;\
    /* semihosting special sequence */ \
    slli x0, x0, 0x1f          ;\
    ebreak                     ;\
    srai x0, x0, 7             ;\
    .option pop

#else /* RV32 */

  # Terminate test with a pass indication.
  # When the test is run in simulation, this should end the simulation.
  #define RVMODEL_HALT_PASS     \
    .option push               ;\
    .option norvc              ;\
    /* load exit code (see above) */ \
    li a1, 0x20026             ;\
    li a0, 0x18                ;\
    /* align to ensure semihosting instructions don't cross a page boundary */ \
    .balign 16                 ;\
    /* semihosting special sequence */ \
    slli x0, x0, 0x1f          ;\
    ebreak                     ;\
    srai x0, x0, 7             ;\
    .option pop

  # Terminate test with a fail indication.
  # When the test is run in simulation, this should end the simulation.
  #define RVMODEL_HALT_FAIL     \
    .option push               ;\
    .option norvc              ;\
    /* load exit code (see above) */ \
    li a1, 0x20023             ;\
    li a0, 0x18                ;\
    /* align to ensure semihosting instructions don't cross a page boundary */ \
    .balign 16                 ;\
    /* semihosting special sequence */ \
    slli x0, x0, 0x1f          ;\
    ebreak                     ;\
    srai x0, x0, 7             ;\
    .option pop

#endif

##### IO #####

# Example UART implementation.
# Expects a PC16550-compatible UART.
# Change these addresses to match your memory map
.EQU UART_BASE_ADDR, 0x10000000
.EQU UART_THR, (UART_BASE_ADDR + 0)
.EQU UART_LCR, (UART_BASE_ADDR + 3)
.EQU UART_LSR, (UART_BASE_ADDR + 5)

# Initialization steps needed prior to writing to the console
# _R1, _R2, and _R3 can be used as temporary registers if needed.
# Do not modify any other registers (or make sure to restore them).
# Can be empty or left undefined if no initialization is needed.
#define RVMODEL_IO_INIT(_R1, _R2, _R3) \
  uart_init:                ;\
    li _R1, UART_LCR         ; /* Load address of UART LCR */    \
    li _R2, 3                ; /* 8-bit characters, 1 stop bit, no parity */ \
    sb _R2, 0(_R1)           ; \

# Prints a null-terminated string using a DUT specific mechanism.
# A pointer to the string is passed in _STR_PTR.
# _R1, _R2, and _R3 can be used as temporary registers if needed.
# Do not modify any other registers (or make sure to restore them).
#define RVMODEL_IO_WRITE_STR(_R1, _R2, _R3, _STR_PTR)               \
1:                           ;                       \
  lbu _R1, 0(_STR_PTR)        ;/* Load byte */        \
  beqz _R1, 3f                ;/* Exit if null */     \
2: /* uart_putc */           ;                      \
  li _R2, UART_LSR ;\
  4: /* uart_putc_wait_busy */ \
    lbu _R3, 0(_R2) ;\
    andi _R3, _R3, 0x20 ;/* check line status register bit 5 */ \
    beqz _R3, 4b ;/* wait until Transmit Holding Register Empty is set */ \
  /* uart_putc_send */ \
    li _R2, UART_THR ; /* transmit character */ \
    sb _R1, 0(_R2) ;\
  addi _STR_PTR, _STR_PTR, 1 ;/* Next char */        \
  j 1b                       ;/* Loop */             \
3:


##### Access Fault #####

#define RVMODEL_ACCESS_FAULT_ADDRESS 0x00000100

##### Machine Timer #####

#define RVMODEL_MTIME_ADDRESS  0x0200BFF8  /* Address of mtime CSR */

#define RVMODEL_MTIMECMP_ADDRESS 0x02004000 /* Address of mtimecmp CSR */

##### Machine Interrupts #####

#define RVMODEL_INTERRUPT_LATENCY 10

#define RVMODEL_TIMER_INT_SOON_DELAY 100

// QEMU virt CLINT runs at 10 MHz; with -icount shift=1 (2 ns/insn) that is ~50 insns/tick.
// Define a 50x multiplier to convert between timer tick and processor cycle count.
#define RVMODEL_MAX_CYCLES_PER_TIMER_TICK 50

#define CLINT_BASE_ADDRESS 0x02000000
#define RVMODEL_MSIP_ADDRESS (CLINT_BASE_ADDRESS + 0x0)

#define PLIC_BASE_ADDRESS    0x0c000000
#define PLIC_ENABLE_ADDRESS  0x0c002000
#define PLIC_THRESH_ADDRESS  0x0c200000
#define PLIC_CLAIM_ADDRESS   0x0c200004
#define PLIC_SENABLE_ADDRESS 0x0c002080   /* For S mode */
#define PLIC_STHRESH_ADDRESS 0x0c201000
#define PLIC_SCLAIM_ADDRESS  0x0c201004
#define NS16550_BASE_ADDRESS 0x10000000
#define UART_INT_SRC         10           /* NS16550 interrupt source ID in QEMU virt */



/* Generates machine external interrupt via PLIC + NS16550 UART transmitter interrupt.
 * The UART throws an interrupt because the THR (Transmit Holding Register) defaults to empty.
 * Steps:
 * - Configures PLIC UART_INT_SRC to priority 7
 * - Enables UART_INT_SRC in PLIC
 * - Sets PLIC priority threshold to 0
 * - Sets UART IER.ETBEI (Interrupt Enable Register - Enable Transmitter Holding Register Empty Interrupt)
 * The PLIC sees that the UART source is enabled and has priority greater than the threshold,
 * so PLIC asserts Machine External Interrupt. */
#define RVMODEL_SET_MEXT_INT(_R1, _R2)          \
  li _R1, 7;                                     \
  li _R2, PLIC_BASE_ADDRESS;                     \
  sw _R1, (4*UART_INT_SRC)(_R2);                 \
  li _R1, (1 << UART_INT_SRC);                   \
  li _R2, PLIC_ENABLE_ADDRESS;                   \
  sw _R1, 0(_R2);                                \
  li _R2, PLIC_THRESH_ADDRESS;                   \
  sw zero, 0(_R2);                               \
  li _R1, 0x02;                                  \
  li _R2, NS16550_BASE_ADDRESS;                  \
  sb _R1, 1(_R2);

/* Clears machine external interrupt:
 * - Turns off UART interrupt by disabling IER.ETBEI
 * - Reads the PLIC Claim register and writes it back to deassert Machine External Interrupt */
#define RVMODEL_CLR_MEXT_INT(_R1, _R2)          \
  li _R2, NS16550_BASE_ADDRESS;                  \
  sb zero, 1(_R2);                               \
  li _R2, PLIC_CLAIM_ADDRESS;                    \
  lw _R1, 0(_R2);                               \
  sw _R1, 0(_R2);                                 \
  li _R2, PLIC_ENABLE_ADDRESS;  /* Since SEXT and MEXT interrupt contexts share the same source, PLIC must be disabled for MEXT context so that it can properly trigger SEXT */\
  sw zero, 0(_R2);

#define RVMODEL_SET_MSW_INT(_R1, _R2) \
  li _R1, 1; \
  li _R2, RVMODEL_MSIP_ADDRESS; \
  sw _R1, 0(_R2);

#define RVMODEL_CLR_MSW_INT(_R1, _R2) \
  li _R2, RVMODEL_MSIP_ADDRESS; \
  sw zero, 0(_R2);

##### Supervisor Interrupts #####

#define RVMODEL_SET_SEXT_INT(_R1, _R2)          \
  li _R1, 7;                                     \
  li _R2, PLIC_BASE_ADDRESS;                     \
  sw _R1, (4*UART_INT_SRC)(_R2);                 \
  li _R1, (1 << UART_INT_SRC);                   \
  li _R2, PLIC_SENABLE_ADDRESS;                   \
  sw _R1, 0(_R2);                                \
  li _R2, PLIC_STHRESH_ADDRESS;                   \
  sw zero, 0(_R2);                               \
  li _R1, 0x02;                                  \
  li _R2, NS16550_BASE_ADDRESS;                  \
  sb _R1, 1(_R2);

#define RVMODEL_CLR_SEXT_INT(_R1, _R2)          \
  li _R2, NS16550_BASE_ADDRESS;                  \
  sb zero, 1(_R2);                               \
  li _R2, PLIC_SCLAIM_ADDRESS;                    \
  lw _R1, 0(_R2);                               \
  sw _R1, 0(_R2);                               \
  li _R2, PLIC_SENABLE_ADDRESS;                 \
  sw zero, 0(_R2);

#define RVMODEL_SET_SSW_INT(_R1, _R2)

#define RVMODEL_CLR_SSW_INT(_R1, _R2)

##### Sscofpmf #####

#define RVMODEL_MHPMCOUNTER    CSR_MHPMCOUNTER3
#define RVMODEL_MHPMEVENT      CSR_MHPMEVENT3
#define RVMODEL_MHPMEVENT_VAL  0x02   // RISCV_PMU_EVENT_HW_INSTRUCTIONS
#define RVMODEL_MHPMEVENT_CODE \
    nop

#endif // _RVMODEL_MACROS_H
